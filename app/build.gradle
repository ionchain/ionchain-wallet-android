apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion extCompileSdkVersion
    buildToolsVersion extBuildToolsVersion

    defaultConfig {
        applicationId "org.ionchain.wallet"
        minSdkVersion extMinSdkVersion
        targetSdkVersion extTargetSdkVersion
        versionCode appVersionCode
        versionName "${appVersionName}"//appVersionName
        multiDexEnabled true //需要将dex 分包  false 则不分
        javaCompileOptions { annotationProcessorOptions { includeCompileClasspath = true } }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }


    signingConfigs {
        key {
            storeFile file("ionchainwallet.keystore")//签名文件
            storePassword "ionchainandroid"
            keyAlias "ionchain"
            keyPassword "ionchainandroid"//签名密码
            v2SigningEnabled false
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.key
            buildConfigField('boolean', 'LOG_DEBUG', 'true')
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.key
            buildConfigField('boolean', 'LOG_DEBUG', 'false')
        }
    }

    flavorDimensions "ionchain"
    productFlavors {
        ionchain {
            dimension "ionchain"
        }
        xiaomi {
            dimension "ionchain"
//            applicationIdSuffix ".demo"
//            versionNameSuffix "-demo"
        }
        huawei {
            dimension "ionchain"
//            applicationIdSuffix ".full"
//            versionNameSuffix "-full"
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            variant.productFlavors.each { flavor ->
                def project = "ionchain"
                def separator = "_"
                def buildType = variant.variantData.variantConfiguration.buildType.name
                def versionName = variant.versionName
                def versionCode = variant.versionCode
                def date = new Date()
                def formattedDate = date.format('yyyyMMdd')

                def apkName = project + separator + "v" + versionName + separator + versionCode + separator + buildType + separator + flavor.name + separator + formattedDate + ".apk"
                output.outputFileName = apkName
            }
        }
    }


    dexOptions {
        maxProcessCount 4 // this is the default value
        javaMaxHeapSize "4g"
    }

    lintOptions {
        // true--错误发生后停止gradle构建
        abortOnError false
        // true--只报告error
        ignoreWarnings false
        // true--所有warning当做error
        warningsAsErrors false
        disable 'GoogleAppIndexingWarning'
    }
    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
    }

}

repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    //    数据库查看（正式版要删除）
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //    二维码
    //    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.2.1'
    //    数字钱包
    implementation 'org.web3j:core:3.3.1-android'
    implementation 'org.bitcoinj:bitcoinj-core:0.14.7'
    //    网络请求框架
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.lzy.net:okserver:2.0.5'
    //    下来刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    //    适配器
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.35'
    //    动态权限申请
    implementation 'pub.devrel:easypermissions:2.0.0'
    //    图片加载
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.github.bumptech.glide:annotations:4.8.0'
    //    数据库管理
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'org.greenrobot:greendao-generator:3.2.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(':ioncwalletsdk')
    //    implementation (name:'ioncwalletsdk-debug',ext:'aar')
    implementation project(':lib-zxing')
    implementation 'com.android.support:multidex:1.0.3'
    implementation files('/Users/binny/AndroidStudioProjects/ionchain/ioncwalletsdk/libs/commons-codec-1.11.jar')
}

apply plugin: 'org.greenrobot.greendao'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.2'
    }
}

//greendao {
//    schemaVersion 1
//    daoPackage 'org.ionchain.wallet.greendao.gen'
//    targetGenDir 'src/main/java'
//}
